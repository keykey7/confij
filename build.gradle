buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
}

plugins {
	id 'org.ajoberstar.reckon' version '0.12.0'
	id 'com.github.ben-manes.versions' version '0.31.0' // task: dependencyUpdates
	id 'org.sonarqube' version '3.0'
	id 'org.asciidoctor.jvm.convert' version '3.2.0' apply false
	id 'com.github.johnrengelman.shadow' version '6.0.0' apply false
}

reckon {
	scopeFromProp()
	stageFromProp('rc', 'final')
}
if (System.getenv('CI') == null) {
	project.version = "0.0.0-SNAPSHOT"
}
task version {
	doLast {
		if (project.findProperty('reckon.stage')) {
			println "to tag and release a new version use:"
			println "git tag -a '${project.version}' -m '${project.version}' && git push origin '${project.version}'"
		} else {
			println "to figure out the next version run:"
			println "CI=true ./gradlew version -Preckon.scope=[major,minor,patch] -Preckon.stage=[rc,final]"
		}
	}
}
reckonTagCreate.dependsOn "build"

allprojects {
	repositories {
		mavenCentral()
	}
	group 'ch.kk7'
	version rootProject.version
}

dependencyUpdates.resolutionStrategy {
	componentSelection { rules ->
		rules.all { ComponentSelection selection ->
			if (['alpha', 'beta', 'cr', 'rc', 'm', 'preview', 'b', 'ea'].any { qualifier ->
				selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
			}) {
				selection.reject('Release candidate')
			}
		}
	}
}

subprojects {
	apply plugin: 'java-library'

	java {
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
		withSourcesJar()
		withJavadocJar()
	}

	tasks.withType(JavaCompile) {
		options.incremental = true
	}

	tasks.withType(AbstractArchiveTask) {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
	}

	configurations.runtime {
		resolutionStrategy {
			failOnVersionConflict()
			failOnNonReproducibleResolution()
		}
	}

	dependencies {
		def autoServiceVersion = "1.0-rc7"
		compileOnly "com.google.auto.service:auto-service:${autoServiceVersion}"
		annotationProcessor "com.google.auto.service:auto-service:${autoServiceVersion}"
		testCompileOnly "com.google.auto.service:auto-service:${autoServiceVersion}"
		testAnnotationProcessor "com.google.auto.service:auto-service:${autoServiceVersion}"

		def lombokVersion = "1.18.16"
		compileOnly "org.projectlombok:lombok:${lombokVersion}"
		annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
		testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
		testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

		def junitVersion = '5.6.2'
		testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
		testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
		testImplementation "org.assertj:assertj-core:3.17.1"
		testImplementation "com.github.stefanbirkner:system-lambda:1.1.0"
		testImplementation "org.awaitility:awaitility:4.0.3"

		testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'
	}

	test {
		useJUnitPlatform() // junit5 support
		if (System.getenv('CI') != null) {
			testLogging {
				events "passed", "skipped", "failed"
				exceptionFormat "full"
			}
		}
	}
	configurations { // allow test reuse
		testOutput.extendsFrom(testImplementation)
	}
}

// coverage
subprojects {
	apply plugin: 'jacoco'
}

apply plugin: 'base'
apply plugin: 'jacoco'
task jacocoMerge(type: JacocoMerge) {
	subprojects.each { subproject ->
		executionData subproject.tasks.withType(Test)
	}
	doFirst {
		executionData = files(executionData.findAll { it.exists() })
	}
}

task jacocoRootReport(type: JacocoReport, group: 'verification') {
	description = 'Generates an aggregate report from all subprojects'
	dependsOn subprojects.test, jacocoMerge

	additionalSourceDirs.from(subprojects.sourceSets.main.allSource.srcDirs)
	sourceDirectories.from(subprojects.sourceSets.main.allSource.srcDirs)
	classDirectories.from(subprojects.sourceSets.main.output)
	executionData jacocoMerge.destinationFile
	reports.xml.enabled true
	doLast {
		logger.lifecycle("report at: " + reports.html.entryPoint.toURI())
	}
}

allprojects {
	sonarqube {
		properties {
			property 'sonar.host.url', 'https://sonarcloud.io'
			property 'sonar.organization', 'keykey7-github'
			property 'sonar.login', System.getenv('SONAR_TOKEN')
			property 'sonar.coverage.jacoco.xmlReportPaths', jacocoRootReport.reports.xml.destination
		}
	}
}
// disable sonar analysis without secret (since secrets aren't availabe in PRs from forks)
project.tasks['sonarqube'].enabled System.getenv('SONAR_TOKEN') != null
project.tasks['sonarqube'].dependsOn jacocoRootReport

// releases
subprojects {
	if (sourceSets.main.allSource.isEmpty()) {
		return // excluding test-only subprojects
	}
	apply plugin: 'maven-publish'
	def isRemotePublish = System.getenv('CI') != null && System.getenv('SONATYPE_USERNAME') != null
	publishing {
		publications {
			java(MavenPublication) {
				from components.java
				pom {
					def githubUrl = "https://github.com/keykey7/confij"
					name = project.name
					description = "A type-safe, strongly defined Java configuration library"
					url = githubUrl
					licenses {
						license {
							name = 'The Apache License, Version 2.0'
							url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}
					scm {
						connection = "scm:git:${githubUrl}.git"
						developerConnection = "scm:git:${githubUrl}.git"
						url = githubUrl
					}
					developers {
						developer {
							id = 'keykey7'
							name = 'keykey7'
							email = 'noreply@kk7.ch'
						}
					}
				}
			}
		}
		if (isRemotePublish) {
			repositories {
				maven {
					def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
					def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
					def isReleaseBuild = project.version.toString().matches("[0-9.]+") // no "rc" or "snapshot"
					url = isReleaseBuild ? snapshotsRepoUrl : releasesRepoUrl
					credentials {
						username = System.getenv('SONATYPE_USERNAME')
						password = System.getenv('SONATYPE_PASSWORD')
					}
				}
			}
		}
	}
	if (isRemotePublish) {
		apply plugin: 'signing'
		signing {
			def signingKey = System.getenv('SONATYPE_SIGNKEY')
			def signingPassword = System.getenv('SONATYPE_SIGNPASS')
			useInMemoryPgpKeys(signingKey, signingPassword)
			sign publishing.publications.java
		}
	}
}
