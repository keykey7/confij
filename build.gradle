buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
}

plugins {
	id 'test-report-aggregation'
	id 'jacoco-report-aggregation'
	id 'org.ajoberstar.reckon' version '0.16.1'
	id 'org.sonarqube' version '3.3'
	id 'com.github.ben-manes.versions' version '0.42.0' apply false // task: dependencyUpdates
	id 'org.asciidoctor.jvm.convert' version '3.3.2' apply false
	id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
	id 'de.marcphilipp.nexus-publish' version '0.4.0' apply false
	id 'io.codearte.nexus-staging' version '0.30.0' apply false
}

reckon {
	scopeFromProp()
	snapshotFromProp()
}
if (System.getenv('CI') == null) {
	version = "0.0.0-SNAPSHOT"
}
task version {
	doLast {
		if (project.findProperty('reckon.stage')) {
			println "to tag and release a new version '${version}' use:"
			println "git tag -a '${version}' -m '${version}' && git push origin '${version}'"
		} else {
			println "current version is '${version}', to figure out the next version run:"
			println "CI=true ./gradlew version -Preckon.stage=final -Preckon.scope=[major,minor,patch]"
		}
	}
}
reckonTagCreate.dependsOn "build"

allprojects {
	repositories {
		mavenCentral()
	}
	group 'ch.kk7'
	version rootProject.version
}

// java defaults
subprojects {
	if (project.name == "confij-bom") {
		return
	}
	apply plugin: 'java-library'
	apply plugin: 'jacoco'
	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(8)
		}
		withSourcesJar()
		withJavadocJar()
	}
	[11, 16].forEach(jv -> {
		def testJava = tasks.register("testJava${jv}", Test) {
			javaLauncher = javaToolchains.launcherFor {
				languageVersion = JavaLanguageVersion.of(jv)
			}
			shouldRunAfter 'test'
		}
		check.dependsOn testJava
	})
	tasks.withType(JavaCompile) {
		options.incremental = true
	}
	tasks.withType(AbstractArchiveTask) {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
	}
	javadoc.options.addStringOption('Xdoclint:none', '-quiet')
	configurations {
		bom
		testOutput.extendsFrom(testImplementation) // reuse tests directly
	}
	configurations.configureEach() {
		if(it.name != 'bom'){
			it.extendsFrom(configurations.bom)
		}
	}
	configurations.all {
		resolutionStrategy {
			failOnVersionConflict()
			failOnNonReproducibleResolution()
		}
	}
	dependencies {
		bom platform(project(':confij-bom'))

		compileOnly "com.google.auto.service:auto-service"
		annotationProcessor "com.google.auto.service:auto-service"
		testCompileOnly "com.google.auto.service:auto-service"
		testAnnotationProcessor "com.google.auto.service:auto-service"

		compileOnly "org.projectlombok:lombok"
		annotationProcessor "org.projectlombok:lombok"
		testCompileOnly "org.projectlombok:lombok"
		testAnnotationProcessor "org.projectlombok:lombok"

		testImplementation "org.junit.jupiter:junit-jupiter-params"
		testImplementation "org.junit.jupiter:junit-jupiter-engine"
		testImplementation "org.assertj:assertj-core"
		testImplementation "com.github.stefanbirkner:system-lambda"
		testImplementation "org.awaitility:awaitility"

		testRuntimeOnly 'ch.qos.logback:logback-classic'
	}
	tasks.withType(Test) {
		useJUnitPlatform() // junit5 support
		if (System.getenv().containsKey("CI")) {
			testLogging {
				events "failed"
				exceptionFormat "full"
			}
		}
	}
}

// coverage
dependencies {
	jacocoAggregation subprojects
}
reporting {
	reports {
		testCodeCoverageReport(JacocoCoverageReport) {
			testType = TestSuiteType.UNIT_TEST
		}
	}
}
sonarqube {
	properties {
		property 'sonar.host.url', 'https://sonarcloud.io'
		property 'sonar.organization', 'keykey7-github'
		property 'sonar.login', System.getenv('SONAR_TOKEN')
		property 'sonar.coverage.jacoco.xmlReportPaths', tasks.testCodeCoverageReport.reports.xml.outputLocation.get()
	}
}
// disable sonar analysis without secret (since secrets aren't availabe in PRs from forks)
tasks.sonarqube.enabled System.getenv('SONAR_TOKEN') != null
tasks.sonarqube.dependsOn tasks.testCodeCoverageReport

// releases
def isRemotePublish = System.getenv('CI') != null && System.getenv('SONATYPE_USERNAME') != null
if (isRemotePublish) {
	logger.lifecycle("remote publishing is enabled");
	if (version.toString().endsWith("-SNAPSHOT")) {
		task closeAndReleaseRepository {} // noop for snapshots
	} else {
		apply plugin: 'io.codearte.nexus-staging'
		nexusStaging {
			username = System.getenv('SONATYPE_USERNAME')
			password = System.getenv('SONATYPE_PASSWORD')
			numberOfRetries = 60
			delayBetweenRetriesInMillis = 5000
		}
	}
}
subprojects {
	if (project.name == "confij-bom" || sourceSets.main.allSource.isEmpty()) {
		return // excluding test-only subprojects
	}
	apply plugin: 'maven-publish'
	publishing {
		publications {
			java(MavenPublication) {
				from components.java
				pom {
					def githubUrl = "https://github.com/keykey7/confij"
					name = project.name
					description = "A type-safe, strongly defined Java configuration library"
					url = githubUrl
					licenses {
						license {
							name = 'The Apache License, Version 2.0'
							url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}
					scm {
						connection = "scm:git:${githubUrl}.git"
						developerConnection = "scm:git:${githubUrl}.git"
						url = githubUrl
					}
					developers {
						developer {
							id = 'keykey7'
							name = 'keykey7'
							email = 'noreply@kk7.ch'
						}
					}
				}
			}
		}
	}
	if (isRemotePublish) {
		apply plugin: 'de.marcphilipp.nexus-publish'
		nexusPublishing {
			repositories {
				sonatype {
					username = System.getenv('SONATYPE_USERNAME')
					password = System.getenv('SONATYPE_PASSWORD')
				}
			}
		}
		apply plugin: 'signing'
		signing {
			def signingKey = System.getenv('SONATYPE_SIGNKEY')
			def signingPassword = System.getenv('SONATYPE_SIGNPASS')
			useInMemoryPgpKeys(signingKey, signingPassword)
			sign publishing.publications.java
		}
	}
}
