plugins {
	id 'base'
	id 'java-platform'
	id 'com.github.ben-manes.versions'
}

dependencies {
	constraints {
		api 'com.fasterxml:classmate:1.5.1'
		api 'com.typesafe:config:1.4.2'
		api('org.slf4j:slf4j-api') {
			version {
				strictly '1.7.36'
			}
		}
		api 'com.github.valfirst:slf4j-test:2.5.0'
		api 'org.tomlj:tomlj:1.0.0'
		api 'org.yaml:snakeyaml:1.30'
		def hibernateValidatorVersion = "7.0.4.Final"
		api "org.hibernate.validator:hibernate-validator:${hibernateValidatorVersion}"
		api "org.hibernate.validator:hibernate-validator-annotation-processor:${hibernateValidatorVersion}"
		def gitVersion = "5.13.0.202109080827-r"
		api "org.eclipse.jgit:org.eclipse.jgit:${gitVersion}"
		api "org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:${gitVersion}"
		api "org.eclipse.jgit:org.eclipse.jgit.junit.http:${gitVersion}"
		api "org.eclipse.jgit:org.eclipse.jgit.junit.ssh:${gitVersion}"
		api "com.google.auto.service:auto-service:1.0.1"
		api "org.projectlombok:lombok:1.18.22"
		def junitVersion = '5.8.2'
		api "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
		api "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
		api "org.assertj:assertj-core:3.22.0"
		api "com.github.stefanbirkner:system-lambda:1.2.1"
		api "org.awaitility:awaitility:4.2.0"
		api('com.google.guava:guava') {
			version {
				strictly '31.0.1-jre'
			}
		}
		runtime 'ch.qos.logback:logback-classic:1.2.11'
	}
}

dependencyUpdates{
	checkConstraints = true
	resolutionStrategy {
		componentSelection { rules ->
			rules.all { ComponentSelection selection ->
				if (['alpha', 'beta', 'cr', 'rc', 'm', 'preview', 'b', 'ea'].any { qualifier ->
					selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
				}) {
					selection.reject('Release candidate')
				}
			}
		}
	}
}
